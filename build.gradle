plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

version = "1.3.2-SNAPSHOT"
group = "com.path-variable"

repositories {
    mavenCentral()
}

dependencies {
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.12'
    implementation 'ch.qos.logback:logback-classic:1.2.6'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.11.3'
    testImplementation group: 'junit', name: 'junit', version: '4.13.1'
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = group
            artifactId = project.name
            version = version

            from components.java
            pom {
                name = "Commons Library"
                description = "A libary including some functionality shared across Path Variable projects"
            }
        }
    }

    repositories {
        maven {
            name = "MavenCentral"
            url = project.version.contains("-SNAPSHOT") ?
                    "https://s01.oss.sonatype.org/content/repositories/snapshots/" :
                    "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = property("mc.user")
                password = property("mc.pass")
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(findProperty("pv.gpg.key"), findProperty("pv.key.pwd"))
    sign publishing.publications.maven
}
